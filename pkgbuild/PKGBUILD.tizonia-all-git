# Maintainer: Juan A. Rubio <jarubio2001@gmail.com>

_githubname=tizonia-openmax-il
pkgname=tizonia-all-git
pkgver=0.8.0.r42.gf2b64089
pkgrel=1
pkgdesc="Command-line music player for Linux with Spotify, Google Play Music, YouTube, SoundCloud, and Dirble support"
arch=('x86_64')
url="https://www.tizonia.org"
license=('LGPL')
depends=(
    # official repositories:
    'libmad'
    'sqlite'
    'libutil-linux'
    'taglib'
    'mediainfo'
    'sdl'
    'lame'
    'faad2'
    'libcurl-gnutls'
    'libvorbis'
    'libvpx'
    'mpg123'
    'opus'
    'opusfile'
    'libogg'
    'libfishsound'
    'flac'
    'liboggz'
    'libsndfile'
    'alsa-lib'
    'libpulse'
    'boost'
    'dbus-c++'
    'check'
    'python2-pafy'
    'python2-simplejson'
    'python2-pycryptodomex'
    'python2-eyed3'
    'python2-pykka'
    'python2-pathlib'
    'python2-crypto'

    # AUR:
    'log4c'
    'libspotify'
    'python2-gmusicapi'
    'python-soundcloud-git'
    'python2-youtube-dl-git'
)
provides=('tizonia-all')
conflicts=('tizonia-all')
source=("${pkgname}"::"git+https://github.com/tizonia/${_githubname}.git")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    local _version="$(git tag | sort -Vr | head -n1 | sed 's/^v//')"
    local _revision="$(git rev-list v"${_version}"..HEAD --count)"
    local _shorthash="$(git rev-parse --short HEAD)"
    
    printf '%s.r%s.g%s' "$_version" "$_revision" "$_shorthash"
}

build() {
	cd "$pkgname"
	autoreconf -ifs
	./configure --prefix=/usr
	make
}

package() {
	cd "$pkgname"
	make DESTDIR="$pkgdir/" install
}
